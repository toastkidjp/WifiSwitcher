apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply from: 'signing/config.gradle', to: android

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    byte verMajor  = 1;
    byte verMiddle = 0;
    byte verMinor  = 6;

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = 'tk-bot@api-6327630184664180575-715630.iam.gserviceaccount.com'
            jsonFile = file('signing/Google Play Android Developer-cbf2176b721a.json')
        }
    }

    play {
        track = 'beta'
        untrackOld = true
    }

    defaultConfig {
        applicationId "jp.toastkid.wifi_switcher"
        minSdkVersion 15
        targetSdkVersion 26
        if (verMiddle >= 100 || verMinor >= 100) {
            throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
        }
        versionCode ((verMajor * 10000) + (verMiddle * 100) + verMinor)
        versionName verMajor + "." + verMiddle + "." + verMinor
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    def supportLibVersion = '26.0.2'
    implementation 'com.android.support:appcompat-v7:' + supportLibVersion
    implementation 'com.android.support:design:'       + supportLibVersion

    def firebaseVersion = '11.4.2'
    implementation 'com.google.firebase:firebase-core:'  + firebaseVersion
    implementation 'com.google.firebase:firebase-ads:'   + firebaseVersion
    implementation 'com.google.firebase:firebase-crash:' + firebaseVersion

    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.squareup.okio:okio:1.13.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'com.github.gfx.android.orma:orma:4.2.5'

    annotationProcessor 'com.github.gfx.android.orma:orma-processor:4.2.5'

    def leakcanaryVersion = '1.5.4'
    debugImplementation   'com.squareup.leakcanary:leakcanary-android:'       + leakcanaryVersion
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:' + leakcanaryVersion

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
