apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    byte verMajor  = 1;
    byte verMiddle = 0;
    byte verMinor  = 4;

    defaultConfig {
        applicationId "jp.toastkid.wifi_switcher"
        minSdkVersion 15
        targetSdkVersion 25
        if (verMiddle >= 100 || verMinor >= 100) {
            throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
        }
        versionCode ((verMajor * 10000) + (verMiddle * 100) + verMinor)
        versionName verMajor + "." + verMiddle + "." + verMinor
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    def supportLibVersion = '25.3.1'
    compile 'com.android.support:appcompat-v7:' + supportLibVersion
    compile 'com.android.support:design:'       + supportLibVersion

    def firebaseVersion = '10.2.1'
    compile 'com.google.firebase:firebase-core:'  + firebaseVersion
    compile 'com.google.firebase:firebase-ads:'   + firebaseVersion
    compile 'com.google.firebase:firebase-crash:' + firebaseVersion

    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.squareup.okio:okio:1.13.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'com.github.gfx.android.orma:orma:4.2.5'

    annotationProcessor 'com.github.gfx.android.orma:orma-processor:4.2.5'

    def leakcanaryVersion = '1.5.1'
    debugCompile   'com.squareup.leakcanary:leakcanary-android:'       + leakcanaryVersion
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:' + leakcanaryVersion

    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
